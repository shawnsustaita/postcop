#!/usr/bin/env perl


#    Postcop is an application that monitors Postfix logs and intervenes in a
#    Postfix fashion (ie postmap and postsuper) when mass mailers are detected.
#    Copyright (C) 2014 Shawn Sustaita <shawn.sustaita@gmail.com>
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU Affero General Public License as
#    published by the Free Software Foundation, either version 3 of the
#    License, or (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU Affero General Public License for more details.
#
#    You should have received a copy of the GNU Affero General Public License
#    along with this program.  If not, see <http://www.gnu.org/licenses/>.


### MODULES
use strict;
use warnings;
use version; our $VERSION = qv('0.1.0');
use IO::File;
use File::stat;
use Sys::Syslog             qw/:standard :macros/;
use Fcntl                   qw/:DEFAULT :flock/;
use POSIX                   qw/setsid sysconf _SC_OPEN_MAX WNOHANG/;
use IO::Socket::INET;
use Email::Sender::Simple   qw/try_to_sendmail/;
use Linux::Inotify2;
use DBM::Deep;
use List::MoreUtils         qw/any none/;
use Net::TCPwrappers        qw/RQ_DAEMON RQ_FILE request_init fromhost hosts_access/;
use Config::Std;


### GLOBALS

## Configuration file variables
## Postcop externals
my $debug;
my $foreground;
my $alarm_time;
my $jail_time;
my $send_threshold;
my $syslog_facility;
my $syslog_priority;
my $maillog_file;
my $tmpworkdir;
my $pardon_file;
my $jail_file;
my $pid_file;
my $postfix_access;
my $listen_port;
my $listen_address;
my $from;
my $to;
my $subject;
my $max_peer_connections;
my $tcp_connect_timeout;
my $postmap_cmd;
my $postsuper_cmd;
my $jailing_regexp;
my $maillog_regexp;

## Non-configuration file variables
## Postcop Internals
my $pid_fh;
my $maillog_fh;
my $inotify;
my $listener;
my %sender_mail_ids;
my %sender_mail_logs;
my %sender_mail_counts;
my %pardons;
my %inmates;
my %new_inmates;
my %peer_inmates;
my @peers;
my $syslogging            =  0;
my $done                  =  0;
my $alarm                 =  0;
my $maillog_rotated       =  0;
my $maillog_inode         = -1;


### DECLARATIONS
sub main;
sub initialize;
sub finalize;
sub init_signals;
sub daemonize;
sub create_locked_pid_file;
sub open_file_with_lock;
sub service_peer;
sub tcp_network_server;
sub init_maillog;
sub Log;
sub tie_db_vars;
sub init_db_vars;
sub is_pardoned;
sub is_inmate;
sub is_peer_inmate;
sub process_maillogs;
sub process_pardons;
sub process_inmates;
sub process_peer_inmates;
sub process_new_inmates;
sub process_jail_sentences;
sub postmap;
sub postsuper;
sub notify_admins;
sub Die;
sub parse_config;
sub compile_regexp;


### MAIN
main;


### SUBROUTINES
sub main {
    initialize;
    while ( !$done ) {
        init_db_vars;
        process_maillogs;
        process_pardons;
        process_inmates;
        process_peer_inmates;
        process_new_inmates;
        process_jail_sentences;
        postmap;
        postsuper;
        notify_admins;
    }
    finalize;
    exit 0;
}


sub initialize {
    $0 = 'postcopd';
    parse_config;
    init_signals;
    $| = 1      if  $foreground;  # Autoflush
    daemonize   if !$foreground;
    create_locked_pid_file;
    tcp_network_server;
    init_maillog;
    tie_db_vars;
    Log "Postcop started.";
    Log "Threshold rate set to $send_threshold emails per $alarm_time seconds.";
    Log "Mail log processing is set to $alarm_time second intervals.";
    return;
}


sub finalize {
    Log "Postcop finished.";
    unlink glob "$tmpworkdir/*.db";
    unlink $pid_file;
    kill -15, getpgrp;  # sig TERM process group.
    return;
}


sub init_signals {
    $SIG{HUP} = 'IGNORE';
    $SIG{ALRM} = sub { $alarm = 1; };
    $SIG{TERM} = $SIG{INT} = sub { $done = 1; };
    return;
}


sub daemonize {
    # Be inherited by init.
    my $pid = fork;
    $pid < 0 and Die "fork failed (daemonize): $!";
    $pid > 0 and exit 0;  # Lose parent
  
    # Set sane working directory and umask.
    chdir '/' or Die "chdir failed: $!";
    umask 022;
  
    # Establish a new session.
    my $sid = setsid;
    $sid < 0 and Die "setsid failed: $!";

    # Close whatever files that were previously open.
    my $openmax = sysconf(_SC_OPEN_MAX);
    $openmax > 0 or $openmax = 1024;
    POSIX::close $_ for 0 .. $openmax;
  
    # Open std{in,out,err} from/to /dev/null.
    open STDIN,  '<', '/dev/null' or Die "open STDIN failed (/dev/null): $!";
    open STDOUT, '>', '/dev/null' or Die "open STDOUT failed (/dev/null): $!";
    open STDERR, '>', '/dev/null' or Die "open STDERR failed (/dev/null): $!";

    return;
}


sub create_locked_pid_file {
    ## Ensure only one daemon runs at a time.  This lock lasts for the daemon's life.
    ## $pid_fh is global so the filehandle lock doesn't go out of scope.
    $pid_fh = open_file_with_lock $pid_file, O_RDWR|O_CREAT, LOCK_EX|LOCK_NB;
    $pid_fh->truncate(0)   or Die "truncate failed ($pid_file): $!";
    $pid_fh->print("$$\n") or Die "write failed ($pid_file): $!";
    $pid_fh->flush()       or Die "flush failed ($pid_file): $!";
    return;
}


sub open_file_with_lock {
    my ($file, $open_flags, $lock_type) = @_;
    my $fh = IO::File->new($file, $open_flags) or Die "open failed ($file): $!";
    flock $fh, $lock_type or Die "flock failed ($file): $!";
    return $fh;
}


sub tcp_network_server {
    # Spawn child network server.
    my $pid = fork;
    $pid < 0 and Die "fork failed (tcp_network_server): $!";
    $pid > 0 and return;  # parent
    # child...

    # Adjust signals accordingly.
    $SIG{ALRM} = $SIG{HUP} = 'IGNORE';
    $SIG{TERM} = $SIG{INT} = sub { $done = 1; };

    # Create tcp network server.
    $listener = IO::Socket::INET->new(
        LocalAddr => $listen_address,
        LocalPort => $listen_port,
        Proto     => 'tcp',
        Listen    => SOMAXCONN,
        Reuse     => 1
    );
    $listener or Die "listener failed ($listen_address:$listen_port): $!";

    # KEEPALIVE will detect dead peer connections.
    $listener->setsockopt(SOL_SOCKET, SO_KEEPALIVE, 1)
        or Die "setsockopt failed (SOL_SOCKET:SO_KEEPALIVE): $!";

    # Keep track of the number of peer connections.
    my $peer_connections = 0;

    # Iterative server that spawns worker children.
    while ( !$done ) {
        # Accept new peer connection.
        my $peer = $listener->accept or next;

        # Consult tcpwrappers for access.
        my $req = request_init RQ_DAEMON, $0, RQ_FILE, $peer->fileno
            or Die "request_init failed: $!";
        fromhost($req);
        if (!hosts_access($req)) {  # Deny access verbosely.
            Log 'Peer access denied via tcpwrappers ('.$peer->peerhost.').';
            next;
        }

        # Don't exceed max number of peer connections.
        $peer_connections++;
        if ($peer_connections > $max_peer_connections) {
            Log "maximum peer connections reached ($max_peer_connections).";
            Log "Ignoring connection ($peer->peerhost).";
            $peer_connections--;
            close $peer or Die "close failed (peer): $!";
            next;
        }

        # Service_peer spawns a child worker to service peer.
        service_peer $peer;

        # Reap zombies and adjust peer connection count accordingly.
        $peer_connections-- while (waitpid(-1, WNOHANG) > 0);
    }
    close $listener or Die "close failed (listener): $!";

    # XXX - I'm seeing init inherit postcop proc.  Find where problem
    # resides and fix.  This may or may not be needed.
    #while (wait >= 0) { }

    kill -15, getpgrp;  # sig TERM to process group.

    exit 0;
}


sub service_peer {
    # Grab peer connection.
    my $peer = shift or return;

    # Spawn a child worker to service peer.
    my $pid = fork;
    $pid < 0 and Die "fork failed (service_peer): $!";
    if ($pid > 0) {  # Parent.
        # For child only.  Parent can close.
        close $peer or Die "close failed (parent peer): $!";
        return;
    }
    # Child...

    # For parent only.  Child can close.
    close $listener or Die "close failed (listener): $!";

    # Adjust signals accordingly.
    $SIG{ALRM} = $SIG{HUP} = $SIG{PIPE} = 'IGNORE';
    $SIG{TERM} = $SIG{INT} = sub { $done = 1; };

    Log 'Sharing jailings with peer ('.$peer->peerhost.').';

    # Share your jailings with peer.
    my $jailfh = open_file_with_lock $jail_file, O_RDONLY|O_CREAT, LOCK_SH;
    while (!$done and my $line = <$jailfh>) {
        print $peer $line;
    }
    close $jailfh or Die "close failed ($jail_file): $!";
    close $peer   or Die "close failed (child peer): $!";

    kill -15, getpgrp if $done;  # sig TERM to process group.

    exit 0;
}


sub init_maillog {
    # Open mail log.
    my $open_attempts = 5;
    while ($open_attempts--) {
        $maillog_fh = IO::File->new($maillog_file, O_RDONLY);
        $maillog_fh and last;
        sleep 1;
    }
    $maillog_fh or Die "open failed ($maillog_file): $!";

    # The inode is used to deduce when a log rotation occurs.
    $maillog_inode = stat($maillog_fh)->ino or Die "stat failed: $!";

    # When processing the mail log and there's no activity, go to sleep until
    # a mail log event occurs via the Linux inotify mechanism.  Perhaps this
    # is better than just explicitly sleeping for a set time period.
    $inotify = Linux::Inotify2->new or Die "inotify new error: $!";
    $inotify->watch(readlink('/proc/'.$$.'/fd/'.fileno($maillog_fh)), IN_ALL_EVENTS)
        or Die "inotify watch error: $!";

    # Log rotated so start at beginning (not end) of new log file.
    if ($maillog_rotated) {
        $maillog_rotated = 0;
    }
    # Postcop just started so seek to end of current log file for processing.
    else {
        $maillog_fh->seek(0, SEEK_END) or Die "seek failed ($maillog_file): $!";
    }

    return;
}


sub Log {
    my $log = shift;

    if ($foreground) {
        print '['.scalar(localtime(time)).'] '."$log\n";
        STDOUT->flush;
    }
    else {
        if (!$syslogging) {
            openlog $0, 'pid', $syslog_facility;
            $syslogging = 1;
        }
        syslog $syslog_priority, $log;
    }

    return;
}


sub tie_db_vars {
    # Hashes are tied to files for the purpose of avoiding memory exhaustion.
    tie %sender_mail_counts, 'DBM::Deep', $tmpworkdir.'/sender_mail_counts.db'
        or Die "Tie failed (sender_mail_counts): $!";
    tie %sender_mail_ids, 'DBM::Deep', $tmpworkdir.'/sender_mail_ids.db'
        or Die "Tie failed (sender_mail_ids): $!";
    tie %sender_mail_logs, 'DBM::Deep', $tmpworkdir.'/sender_mail_logs.db'
        or Die "Tie failed (sender_mail_logs): $!";
    tie %inmates, 'DBM::Deep', $tmpworkdir.'/inmates.db'
        or Die "Tie failed (inmates): $!";
    tie %new_inmates, 'DBM::Deep', $tmpworkdir.'/new_inmates.db'
        or Die "Tie failed (new_inmates): $!";
    tie %peer_inmates, 'DBM::Deep', $tmpworkdir.'/peer_inmates.db'
        or Die "Tie failed (peer_inmates): $!";
    tie %pardons, 'DBM::Deep', $tmpworkdir.'/pardons.db'
        or Die "Tie failed (pardons): $!";
    return;
}


sub init_db_vars {
    %sender_mail_counts = ();
    %sender_mail_ids    = ();
    %sender_mail_logs   = ();
    %inmates            = ();
    %new_inmates        = ();
    %peer_inmates       = ();
    %pardons            = ();
    return;
}


sub process_maillogs {
    # Set the interval for monitoring the mail log.
    # Read and process the mail log through interval duration.
    alarm $alarm_time;
    while (!$alarm and !$done) {
        # Attempt to read a line from the mail log.
        my $line = <$maillog_fh>;

        # Handle end of file situation.
        if ($maillog_fh->eof) {
            # Ignore EOF (Clear EOF state).
            $maillog_fh->clearerr;

            # If log rotated, set an indicator flag.
            if ($maillog_file ne readlink('/proc/'.$$.'/fd/'.fileno($maillog_fh))) {
                $maillog_rotated = 1;
            }

            Log "Maillog rotated." if $debug and $maillog_rotated;

            # if EOF and log rotated, open new log.
            if ($maillog_rotated) {
                init_maillog;
            }
            # if EOF and log not rotated, sleep until next log event.
            else {
                $inotify->read;
            }
        }

        # EOF will render an undefined line.  Try read again.
        next if !defined $line;

        # Remove newline.
        chomp $line;

        # Parse log line.  Find and extract the mail ID, sender
        # and number of recipients.  This regular expression is
        # critical to the functionality of Postcop and needs to
        # be tailored to your version of Postfix logs.
        # $maillog_regexp is a global variable that can be found
        # toward the beginning of this Postcop program.
        next if $line !~ $maillog_regexp;

        # Disregard bogus data.  Postcop requires all three.
        next if !defined $1;
        next if !defined $2;
        next if !defined $3;

        # Assign extracted data to appropriate variables.
        my ($mailid, $sender, $nrcpts) = ($1, lc $2, $3);

        # Disregard logs with duplicate mail IDs.
        next if any { $mailid eq $_ } @{ $sender_mail_ids{$sender} };

        # Populate data structures for later processing.
        Log 'push @{ $sender_mail_ids{'.$sender.'} }, '.$mailid if $debug;
        push @{ $sender_mail_ids{$sender}  }, $mailid;

        Log 'push @{ $sender_mail_logs{'.$sender.'} }, '.$line if $debug;
        push @{ $sender_mail_logs{$sender} }, $line;

        Log '$sender_mail_counts{'.$sender.'} += '.$nrcpts if $debug;
        $sender_mail_counts{$sender} += $nrcpts;
    }
    $alarm = 0;  # Reset alarm flag.

    return;
}


sub process_pardons {
    # Postcop is above the law; Pardon $from.
    $pardons{$from} = '1';  # Appease compiler with assignment.

    # Iterate pardons.
    # Parse the pardon file.  File consists of line separated regexps.
    my $pardonfh = open_file_with_lock $pardon_file, O_RDONLY|O_CREAT, LOCK_SH;
    while (my $line = <$pardonfh>) {
        # Remove newline.
        chomp $line;

        # Verify the regular expression is valid.
        eval { my $is_valid_regexp = qr/$line/; };
        if ($@) {
            Log "Invalid regular expression ($pardon_file): $line";
            next;
        }

        # Populate data structures for later processing.
        Log "\$pardons{$line}" if $debug;
        $pardons{$line} = '1';  # Appease compiler with assignment.
    }
    close $pardonfh or Die "close failed ($pardon_file)";

    return;
}


sub process_inmates {
    # Time now is for calculating current jail durations.
    my $now = time;

    # Interate inmates.
    # Determine if inmates are freed or not.
    my $jailfh = open_file_with_lock $jail_file, O_RDONLY|O_CREAT, LOCK_SH;
    while (my $line = <$jailfh>) {
        # Remove newline.
        chomp $line;

        # Match a valid jail entry or ignore it verbosely.
        if ($line !~ $jailing_regexp) {
            Log "Ignoring malformed jail entry: $line";
            next;
        }

        # Assign extracted data to appropriate variables.
        my ($sender, $then) = (lc $1, $2);

        # Verify good data or ignore verbosely.
        if (!defined($sender) and !defined($then)) {
            Log "Ignoring malformed jail entry: $line";
            next;
        }

        # Free inmates that served their jail sentence.
        if ( ($now - $then) >= $jail_time ) {
            Log "$sender has been freed from jail (sentence expired).";
            next;
        }

        # Free inmates that have been pardoned.
        if (is_pardoned($sender)) {
            Log "$sender has been freed from jail (pardoned).";
            next;
        }

        # Populate data structures for later processing.
        Log "\$inmates{$sender} = $line" if $debug;
        $inmates{$sender} = $line;
    }
    close $jailfh or Die "close failed ($jail_file): $!";

    return;
}


sub process_peer_inmates {
    # Time now is for timestamping peer jail transfers.
    my $now = time;

    # Iterate peers.
    # Determine if inmate transfers are required or not.
    PEERS:
    for my $peer (@peers) {
        # Don't query self.
        next PEERS if $peer eq $listen_address;

        # Create TCP network connection to peer.
        my $sock = IO::Socket::INET->new(
            PeerAddr => $peer,
            PeerPort => $listen_port,
            Timeout  => $tcp_connect_timeout,
        );

        # Verify a good socket.
        if (!$sock) {
            Log "sock failed ($peer:$listen_port): $!";
            next PEERS;
        }

        # Iterate peer jailings.
        JAILINGS:
        while (my $line = <$sock>) {
            # Remove newline.
            chomp $line;

            # Jail policies (ie $postfix_access) must be identical for inmate transfers.
            if ($line !~ $jailing_regexp) {
                Log "Ignoring malformed jail entry: $line";
                next JAILINGS;
            }
            
            # Assign extracted data to appropriate variables.
            my ($sender, $then) = (lc $1, $2);

            # Verify good data or ignore verbosely.
            if (!defined($sender) and !defined($then)) {
                Log "ignoring malformed jail entry: $line";
                next JAILINGS;
            }

            # Ignore sender if already processed.
            next if is_pardoned($sender);
            next if is_inmate($sender);

            # Populate data structures for later processing.
            Log "$sender has been sentenced jail time via peer collaboration ($peer).";
            Log "\$peer_inmates{$sender} = $sender $postfix_access [$now]" if $debug;
            $peer_inmates{$sender} = "$sender $postfix_access [$now]";
        }  # end JAILINGS

        close $sock;

    }  # end PEERS

    return;
}


sub process_new_inmates {
    # Time now is for timestamping peer jail transfers.
    my $now = time;

    # Iterate new senders.
    # Determine who goes to jail.
    while (my ($sender, $count) = each %sender_mail_counts) {
        # Verify good data.
        next if !defined $sender;
        next if !defined $count;

        # Ignore compliant senders.
        next if $count < $send_threshold;

        # Ignore sender if already processed.
        next if is_pardoned($sender);
        next if is_inmate($sender);
        next if is_peer_inmate($sender);

        Log "$sender has been jailed ($count emails per $alarm_time seconds).";

        if ($debug) {
            while (my (undef, $mail_log) = each @{ $sender_mail_logs{$sender} }) {
                Log $mail_log;
            }
        }

        # Populate data structures for later processing.
        Log "\$new_inmates{$sender} = $sender $postfix_access [$now]" if $debug;
        $new_inmates{$sender} = "$sender $postfix_access [$now]";
    }

    return;
}


sub process_jail_sentences {
    # Iterate through processed data structures and perform jailings.
    # In other words, write the postfix access rules to the postmapable
    # jail file.  The postmap command will later be executed on the jail file
    # to put the new jailing into affect.

    my $jailfh = open_file_with_lock $jail_file, O_WRONLY|O_CREAT|O_TRUNC, LOCK_EX;

    # Jail existing inmates.
    while (my (undef, $jailing) = each %inmates) {
        Log "print \$jailfh $jailing" if $debug;
        print $jailfh "$jailing\n" or Die "write failed ($jail_file): $!";
    }

    # Jail peer inmates.
    while (my (undef, $jailing) = each %peer_inmates) {
        Log "print \$jailfh $jailing" if $debug;
        print $jailfh "$jailing\n" or Die "write failed ($jail_file): $!";
    }

    # Jail new inmates.
    while (my (undef, $jailing) = each %new_inmates) {
        Log "print \$jailfh $jailing" if $debug;
        print $jailfh "$jailing\n" or Die "write failed ($jail_file): $!";
    }

    close $jailfh or Die "close failed ($jail_file): $!";

    return;
}


sub postmap {
    # Implement new jailings via Postfix' postmap command.
    my $ret = system($postmap_cmd);
    Die "system failed ($postmap_cmd): returned $ret: $!"
        if $ret != 0;
    return;
}


sub postsuper {
    # Return if nothing to do.
    return if !%new_inmates;

    # Open pipe to Postfix' postsuper command.
    open my $pipefh, '|-', $postsuper_cmd
        or Die "open pipe failed ($postsuper_cmd): $!";

    # Mail IDs will be written to postsuper via stdin.
    while (my ($new_inmate, undef) = each %new_inmates) {
        my $mail_ids_aref = $sender_mail_ids{$new_inmate};

        while (my (undef, $mail_id) = each @{ $mail_ids_aref }) {
            # Write mail ID to postsuper's stdin via pipe.
            print $pipefh "$mail_id\n";
        }
    }

    close $pipefh or Die "close failed (pipefh): $!";

    return;
}


sub notify_admins {
    # Return if nothing to do.
    return if !%new_inmates;

    # Build email header.
    my $header = [ To      => $to,
                   From    => $from,
                   Subject => $subject, ];

    # Build body; Newline separated list of new jailings.
    my $body;
    while (my ($new_inmate, undef) = each %new_inmates) {
        $body .= "$new_inmate\n";
    }

    # Build complete email with header and body.
    my $email = Email::Simple->create(
        header => $header,
        body   => $body,
    );

    # Best effort attempt to send new jailings to admin address.
    # Utilizes system sendmail command.
    try_to_sendmail($email) or Log "try_to_sendmail failed: $!";

    return;
}


sub Die {
    my $msg = shift;
    Log $msg;
    Log "Postcop died unexpectedly.";
    kill -15, getpgrp;  # Sig TERM process group.
    exit 1;
}


sub is_pardoned {
    my $sender = shift;
    return exists $pardons{$sender};
}


sub is_inmate {
    my $sender = shift;
    return defined $inmates{$sender};
}


sub is_peer_inmate {
    my $sender = shift;
    return defined $peer_inmates{$sender};
}

sub compile_regexp {
    my $regexp = shift // return;
    return eval { qr/$regexp/xmsi };
}

sub parse_config {
    my $config_file = $ARGV[0] // '/etc/postcop/postcopd.conf';
    my %conf;

    eval { read_config $config_file => %conf };
    Die "parse_config failed ($config_file): $@" if $@;

    ## Parse config with sane defaults.

    # Be verbose.
    $debug = $conf{''}{debug}                                  // 0;

    # To daemonize or not to daemonize.
    $foreground = $conf{''}{foreground}                        // 0;

    # Mail logs will be captured for $alarm_time duration
    # (in seconds) intervals before being processed.
    $alarm_time = $conf{''}{alarm_time}                        // 60;

    # If sender hits mail sending threshold in $alarm_time
    # duration, sender is put in jail for $jail_time duration
    # (in seconds) rounded up to next $alarm_time.
    $send_threshold = $conf{''}{send_threshold}                // 100;

    # Amount of time (in seconds) a sender will be in jail.
    $jail_time = $conf{''}{jail_time}                          // 60 * 60;

    # Location of Postfix access(5) file.
    # This is the so-called jail.
    $jail_file = $conf{''}{jail_file}                          // '/etc/postcop/postcop_jailings';

    # Location of Postfix access(5) file.
    # Those above the law.  Those that never do jail time.
    $pardon_file = $conf{''}{pardon_file}                      // '/etc/postcop/postcop_pardons';

    # Postfix access(5) action.  REJECT, DISCARD, HOLD, etc.
    # This is an offender's jail sentence.
    $postfix_access = $conf{''}{postfix_access}                // 'REJECT email rate limit exceeded.';

    # The command that sentences new offenders jail time.
    # This is passed to the perl builtin - system.
    # Postfix needs hooks into this lookup table.
    # perldoc -f system
    # access(5)
    # postmap(1)
    # postconf(5) - smtpd_sender_restrictions
    $postmap_cmd = $conf{''}{postmap_cmd}                      // "/usr/sbin/postmap $jail_file";

    # The command that processes new offenders' queued mail.
    # This command must read from stdin!
    # postsuper(1)
    $postsuper_cmd = $conf{''}{postsuper_cmd}                  // '/usr/sbin/postsuper -h-';

    # Regular expression to parse jail file.
    # Postcop needs to capture inmate and jail sentence (in seconds).
    $jailing_regexp = compile_regexp $conf{''}{jailing_regexp} // qr{
        \A             # Anchor to beginning of srtring
        ([^\s]+)       # Capture inmate (ie sender addr)
        [\s]+          # Whitespace
        .*             # Match anything ($postfix_access)
        [\s]           # Whitespace
        \[ ([0-9]+) \] # Capture jail time seconds
        \s*            # Whitespace
        \z             # Anchor to end of string
    }xmsi;

    # Regular expression to parse Postfix mail logs.
    # This is vital to the proper functioning of postcop.
    # Postcop needs to capture mail ID, sender and number of recipients.
    $maillog_regexp = compile_regexp $conf{''}{maillog_regexp} // qr{
        \A             # Anchor to beginning of srtring
        .*             # Match anything
        \s             # Whitespace
        (.*?):         # Capture mail ID
        \s             # Whitespace
        from=<(.*?)>,  # Capture sender email address
        \s             # Whitespace
        size=.*?,      # Mail size (don't care)
        \s             # Whitespace
        nrcpt=(\d+)    # Capture number of recipients
        .*             # Match anything
        \z             # Anchor to end of string
    }xmsi;

    # Syslog facility (perldoc Sys::Syslog)
    $syslog_facility = $conf{''}{syslog_facility}              // LOG_DAEMON;

    # Syslog priority (perldoc Sys::Syslog)
    $syslog_priority = $conf{''}{syslog_priority}              // LOG_INFO;

    # Postcop will send notifications of new jailing to admins
    # with sender address $from.
    $from = $conf{''}{from}                                    // 'postcop@localhost';

    # Postcop will send notifications of new jailing to admins
    # with recipient address $to.
    $to = $conf{''}{to}                                        // 'root@localhost';

    # Postcop will send notifications of new jailing to admins
    # with subject line $subject.
    $subject = $conf{''}{subject}                              // 'postcop jailings';

    # Location of Postfix mail log.
    $maillog_file = $conf{''}{maillog_file}                    // '/var/log/mail.log';

    # Temporary working directory.  Do not touch!
    $tmpworkdir = $conf{''}{tmpworkdir}                        // '/var/lib/postcop';

    # Location of daemon PID file.
    $pid_file = $conf{''}{pid_file}                            // '/var/run/postcopd.pid';

    # To fight DOS attacks, postcop will only accept
    # $max_peer_connections connections.
    $max_peer_connections = $conf{''}{max_peer_connections}    // 10;

    # Connection timeout when connecting to a peer.
    $tcp_connect_timeout = $conf{''}{tcp_connect_timeout}      // 5;

    # TCP port postcopd will listen on.
    $listen_port = $conf{''}{listen_port}                      // 1234;

    # TCP interface postcopd will listen on.  Lacking a value,
    # postcopd will listen on all interfaces.
    $listen_address = $conf{''}{listen_address}                // undef;

    # Peers that postcop queries for inmates populations.
    if (defined $conf{''}{peer}) {
        if (ref $conf{''}{peer} eq 'ARRAY') {
            @peers = @{ $conf{''}{peer} };
        }
        else {
            push @peers, $conf{''}{peer};
        }
    }

    if ($debug) {
        Log "debug = $debug";
        Log "foreground = $foreground";
        Log "syslog_facility = $syslog_facility";
        Log "syslog_priority = $syslog_priority";
        Log "alarm_time = $alarm_time";
        Log "send_threshold = $send_threshold";
        Log "jail_time = $jail_time";
        Log "maillog_file = $maillog_file";
        Log "tmpworkdir = $tmpworkdir";
        Log "pardon_file = $pardon_file";
        Log "jail_file = $jail_file";
        Log "pid_file = $pid_file";
        Log "postfix_access = $postfix_access";
        Log "listen_port = $listen_port";
        Log "listen_address = " . ($listen_address ?  $listen_address : '0.0.0.0');
        Log "from = $from";
        Log "to = $to";
        Log "subject = $subject";
        Log "max_peer_connections = $max_peer_connections";
        Log "tcp_connect_timeout = $tcp_connect_timeout";
        Log "postmap_cmd = $postmap_cmd";
        Log "postsuper_cmd = $postsuper_cmd";
        Log "jailing_regexp = $jailing_regexp";
        Log "maillog_regexp = $maillog_regexp";
        Log "peer = $_" for @peers;
    }

    return;
}
